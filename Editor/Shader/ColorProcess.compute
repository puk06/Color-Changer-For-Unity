#pragma kernel CSMain

#include "BalanceModeShader.hlsl"
#include "AdvancedColorShader.hlsl"

RWTexture2D<float4> _Source;
RWTexture2D<float4> _Target;

// Mask Texture
bool _useMask;
RWTexture2D<float4> _maskTexture;
int _maskSelectionType;

int3 _prevColor;
int3 _ColorOffset;

// Balance Mode
bool _balanceModeEnabled;
int _balanceModeVersion;
float _balanceModeV1Weight;
float _balanceModeV1MinimumValue;
float _balanceModeV2Weight;
float _balanceModeV2Radius;
float _balanceModeV2MinimumValue;
bool _balanceModeV2IncludeOutside;
RWTexture2D<float4> _balanceModeV3Gradient;
int _balanceModeV3GradientResolution;

// Advanced Color Settings
bool _advancedColorModeEnabled;
float _advancedColorSettingsBrightness;
float _advancedColorSettingsContrast;
float _advancedColorSettingsGamma;
float _advancedColorSettingsExposure;
float _advancedColorSettingsTransparency;

bool IsMaskSelected(uint2 id)
{
    uint width, height;
    _maskTexture.GetDimensions(width, height);
    if (id.x >= width || id.y >= height) return false;
    int4 color = (int4)(_maskTexture[id.xy] * 255.0);

    if (_maskSelectionType == 1) return color.r == 0 && color.g == 0 && color.b == 0;
    if (_maskSelectionType == 2) return color.r == 255 && color.g == 255 && color.b == 255;
    if (_maskSelectionType == 3) return color.a == 255;
    if (_maskSelectionType == 4) return color.a != 0;
    if (_maskSelectionType == 5) return color.a == 0;

    return false;
}

[numthreads(16, 16, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    uint width, height;
    _Target.GetDimensions(width, height);
    if (id.x >= width || id.y >= height) return;

    if (_useMask && !IsMaskSelected(id.xy))
    {
        _Target[id.xy] = _Source[id.xy];
        return;
    }

    int4 color = (int4)(_Source[id.xy] * 255.0);

    if (_balanceModeEnabled)
    {
        color.rgb = BalanceColorAdjustment(
            color.rgb, _prevColor, _ColorOffset,
            _balanceModeVersion,
            _balanceModeV1Weight,
            _balanceModeV1MinimumValue,
            _balanceModeV2Radius,
            _balanceModeV2Weight,
            _balanceModeV2MinimumValue,
            _balanceModeV2IncludeOutside,
            _balanceModeV3Gradient,
            _balanceModeV3GradientResolution
        );
    }
    else
    {
        color.rgb += _ColorOffset;
    }

    if (_advancedColorModeEnabled)
    {
        color = AdvancedColorAdjustment(
            color,
            _advancedColorSettingsBrightness,
            _advancedColorSettingsContrast,
            _advancedColorSettingsGamma,
            _advancedColorSettingsExposure,
            _advancedColorSettingsTransparency
        );
    }
    
    _Target[id.xy] = saturate(color / 255.0);
}
